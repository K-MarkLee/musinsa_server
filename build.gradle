plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}


group = 'com.musinsa'
version = '0.0.1-SNAPSHOT'
description = 'server'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot Starters
    implementation "me.paulschwarz:spring-dotenv:4.0.0"
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    // MyBatis
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.5'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'


    //QueryDSL
    implementation 'com.querydsl:querydsl-jpa:5.1.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:5.1.0:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // Database
    runtimeOnly 'com.mysql:mysql-connector-j'

    // Jaeger
    implementation 'io.micrometer:micrometer-observation'
    implementation("io.micrometer:micrometer-tracing-bridge-otel")
    implementation("io.opentelemetry:opentelemetry-exporter-otlp")

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    // Swagger (SpringDoc OpenAPI)
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.5'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.batch:spring-batch-test'
    runtimeOnly 'com.h2database:h2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.reflections:reflections:0.10.2'

    // Test - Lombok
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // https://mvnrepository.com/artifact/com.google.firebase/firebase-admin
    implementation 'com.google.firebase:firebase-admin:9.7.0'

    implementation platform("software.amazon.awssdk:bom:2.25.65") // 버전은 상황에 맞게
    implementation "software.amazon.awssdk:s3"

    // https://mvnrepository.com/artifact/com.squareup.okhttp3/okhttp
    implementation 'com.squareup.okhttp3:okhttp:5.2.1'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    // broker test
    implementation 'org.springframework.kafka:spring-kafka'               // KafkaTemplate
    implementation 'org.springframework.boot:spring-boot-starter-amqp'    // AmqpTemplate (RabbitMQ)
    implementation 'org.springframework.boot:spring-boot-starter-reactor-netty'

    // redission (분산락)

    implementation 'org.redisson:redisson-spring-boot-starter:3.23.5'
    // elasticsearch
    implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch'

    // Apache HttpClient (AWS SigV4 인터셉터에서 사용)
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'

}


jacoco {
    toolVersion = "0.8.13"
    reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

tasks.named('test') {
    useJUnitPlatform()
    // ignoreFailures = true
    finalizedBy jacocoTestReport
}

def jacocoIncludes = ['com/mudosa/musinsa/product/**']
def jacocoExcludes = ['**/Q*.*']

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacoco')
    }

    dependsOn test

    classDirectories.setFrom(
            files(classDirectories.files.collect { dir ->
                fileTree(
                        dir: dir,
                        include: jacocoIncludes,
                        exclude: jacocoExcludes
                )
            })
    )
}


jacocoTestCoverageVerification {

    classDirectories.setFrom(
            files(classDirectories.files.collect { dir ->
                fileTree(
                        dir: dir,
                        include: jacocoIncludes,
                        exclude: jacocoExcludes
                )
            })
    )

    violationRules {
        rule {
            enabled = true
            element = 'CLASS' // 클래스 단위 기준

            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 0.5
            }
        }
    }
}


clean {
    delete file('src/main/generated')
}
